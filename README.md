## Overview

`rules_rs` is a Rust + Bazel ruleset built on top of [rules_rust](https://github.com/bazelbuild/rules_rust).
It provides a redistribution of the core compilation rules from `rules_rust`, augmenting them with optimized toolchains, crates.from_cargo integration, and other codepaths.

## Why `rules_rs`

- Fast incremental dependency resolution via Bazel downloader integration and lockfile facts. It uses your Cargo lockfile directly, with no Cargo workspace splicing and no Bazel-specific Cargo lockfile.
- Hermetic Rust toolchains covering a wide target matrix, including Linux GNU/musl and Windows MSVC/GNU/GNULVM ABI variants.
- Cross builds from any supported host to any supported target through the `@llvm` toolchain, including remote execution use cases.
- A patched `rules_rust` repository with compatibility fixes for Windows linking, rust-analyzer integration, and related workflows.

# Installation And Configuration

Add `rules_rs` to `MODULE.bazel`:

```bzl
bazel_dep(name = "rules_rs", version = "0.0.33")
```

## Paved Path

This is the default setup for new users. It provisions the patched `rules_rust`, registers `rules_rs` Rust toolchains, sets explicit host platforms, and resolves Cargo dependencies against `rules_rs` platforms.

### `MODULE.bazel`

```bzl
bazel_dep(name = "rules_rs", version = "0.0.61")
bazel_dep(name = "llvm", version = "0.7.7")
bazel_dep(name = "platforms", version = "1.1.0")

toolchains = use_extension("@rules_rs//rs/toolchains:module_extension.bzl", "toolchains")
toolchains.toolchain(
    edition = "2024",
    version = "1.92.0",
)
use_repo(toolchains, "default_rust_toolchains")

# This extension is optional but can help keep existing `@rules_rust` references working.
rules_rust = use_extension("@rules_rs//rs:rules_rust.bzl", "rules_rust")
use_repo(rules_rust, "rules_rust")

register_toolchains(
    "@default_rust_toolchains//...",
    "@llvm//toolchain:all",
)

crate = use_extension("@rules_rs//rs:extensions.bzl", "crate")
crate.from_cargo(
    name = "crates",
    cargo_lock = "//:Cargo.lock",
    cargo_toml = "//:Cargo.toml",
    platform_triples = [
        "aarch64-apple-darwin",
        "aarch64-pc-windows-msvc",
        "aarch64-unknown-linux-gnu",
        "x86_64-apple-darwin",
        "x86_64-pc-windows-msvc",
        "x86_64-unknown-linux-gnu",
    ],
)
use_repo(crate, "crates")
```

`platform_triples` should include every exec and target triple that can participate in the build. For the common case, include the host triples you use locally and in CI plus the target triples you build for.

### Toolchain declarations in dependencies

When multiple modules declare `toolchains.toolchain` with the same `name`
(defaulting to `default_rust_toolchains`), a declaration in the root module
controls the complete configuration. Otherwise, `rules_rs` independently selects
the highest Rust version, edition, rustfmt version, and rust-analyzer version.
Omitted rustfmt and rust-analyzer versions use each declaration's Rust version
before comparison. Stable versions are compared numerically; dated beta or
nightly versions are compared by date within the same channel.

Dependencies that mix stable, beta, or nightly versions, or specify different
`extra_rustc_flags` or `extra_exec_rustc_flags`, must use different toolchain repo
names or be overridden by a root-module declaration. Conflicting declarations
within the root module are errors. Without a root override, any declaration with
`use_rust_redist = False` disables redistribution for the selected versions.

### Rust toolchain archives

Stable Rust toolchains use Zstandard-compressed archives from
[hermeticbuild/rust-redist](https://github.com/hermeticbuild/rust-redist) when
available. Other stable versions and nightly toolchains continue to use
`static.rust-lang.org`.

Set `use_rust_redist = False` to download a toolchain and its configured
rustfmt and rust-analyzer versions directly from `static.rust-lang.org`:

```bzl
toolchains.toolchain(
    edition = "2024",
    version = "1.97.1",
    use_rust_redist = False,
)
```

`MODULE.bazel.lock` records the selected archive filenames and SHA-256 values.
Existing `.tar.xz` archives remain locked to `static.rust-lang.org` after a
redistributed release becomes available. Update the lockfile to use the new
`.tar.zst` archives with:

```shell
bazel mod deps --lockfile_mode=update --repo_env=RULES_RS_RUST_REDIST_REFRESH=1
bazel mod deps --lockfile_mode=update
```

The second command restores the normal environment before the updated lockfile
is committed.

### Global Cargo configuration

The root module can configure a shared Cargo configuration file for every Cargo closure, including closures declared by dependencies:

```bzl
crate = use_extension("@rules_rs//rs:extensions.bzl", "crate")
crate.config(
    cargo_config_toml = "//:.cargo/config.toml",
    use_home_cargo_credentials = True,  # Optional.
)
```

Use `cargo_config_toml` to configure custom Cargo registries such as Artifactory, including `[source.crates-io]` replacements. Set `use_home_cargo_credentials = True` when the registry requires credentials from `~/.cargo/credentials.toml`.

A closure that supplies `crate.from_cargo(cargo_config = ...)` uses its own configuration file instead. Only the root module's `crate.config` applies globally; `crate.config` tags in dependency modules are ignored.

### `.bazelrc`

Linux hosts work with Bazel's default host platform. If you also build on Windows,
set an explicit host platform there so Rust toolchain resolution can choose the
right ABI.

```bazelrc
common --enable_platform_specific_config
common:windows --host_platform=//platforms:local_windows_msvc
```

### `platforms/BUILD.bazel`

```bzl
platform(
    name = "local_windows_msvc",
    parents = ["@platforms//host"],
    constraint_values = [
        "@llvm//constraints/windows/abi:msvc",
    ],
)
```

macOS does not need an additional ABI constraint for the default host case.

### `BUILD.bazel`

Prefer the `rules_rs` wrappers for Rust targets:

```bzl
load("@crates//:defs.bzl", "aliases", "all_crate_deps")
load("@rules_rs//rs:rust_binary.bzl", "rust_binary")
load("@rules_rs//rs:rust_library.bzl", "rust_library")

rust_library(
    name = "lib",
    srcs = ["src/lib.rs"],
    aliases = aliases(),
    deps = all_crate_deps(normal = True),
)

rust_binary(
    name = "app",
    srcs = ["src/main.rs"],
    deps = [":lib"],
)
```

### Cargo lint configuration

Enable Cargo lint configuration for a dependency closure through its
`crate.from_cargo` tag:

```bzl
crate.from_cargo(
    name = "crates",
    cargo_lock = "//:Cargo.lock",
    cargo_toml = "//:Cargo.toml",
    generate_lint_config = True,
    platform_triples = [...],
)
```

When enabled, the crate extension parses each workspace member's `Cargo.toml`
and exposes its effective Cargo lint configuration through the generated
`lint_config()` helper. A member with `[lints] workspace = true` receives
`[workspace.lints]`, a member with package lints receives those lints, and a
member without a `[lints]` table receives no lint configuration.

Pass the generated helper to the existing Rust rule alongside the other
`DEP_DATA` helpers:

```bzl
load(
    "@crates//:defs.bzl",
    "aliases",
    "all_crate_deps",
    "lint_config",
)
load("@rules_rs//rs:rust_library.bzl", "rust_library")

rust_library(
    name = "lib",
    srcs = ["src/lib.rs"],
    aliases = aliases(),
    deps = all_crate_deps(normal = True),
    lint_config = lint_config(),
)
```

The selection follows each member manifest for both virtual and non-virtual
workspaces. Workspace lints are not applied to members that do not opt in.

## rust-analyzer

See the upstream `rules_rust` rust-analyzer docs for editor setup details:
https://bazelbuild.github.io/rules_rust/rust_analyzer.html#vscode

If your root module does not expose rules_rs’s patched rules_rust repository
as `@rules_rust`, replace `@rules_rust//tools/rust_analyzer:setup` in the
upstream instructions with `@rules_rs//tools/rust_analyzer:setup`.

## Advanced Options

<details>
<summary>Use the host macOS SDK</summary>

`rules_rs` uses a hermetic macOS SDK by default. Add this setting to
`.bazelrc` when the build must use the macOS SDK selected by Xcode or the
C/C++ toolchain instead:

```bazelrc
common --@rules_rust//rust/settings:use_hermetic_macos_sdkroot=false
```

</details>

<details>
<summary>Register a custom Rust compiler</summary>

`declare_rustc_toolchains` accepts a custom compiler and reuses the generated
toolchain's standard libraries, rustdoc, Cargo, Clippy, and linkers.

Create a dedicated `toolchains/BUILD.bazel` package:

```bzl
load("@rules_rs//rs/toolchains:declare_rustc_toolchains.bzl", "declare_rustc_toolchains")

declare_rustc_toolchains(
    name = "custom_rust",
    edition = "2024",
    rustc = {
        "aarch64-apple-darwin": "//tools/rust:rustc_macos_arm64",
        "x86_64-unknown-linux-gnu": "//tools/rust:rustc_linux_x86_64",
    },
    version = "1.92.0",
)
```

A `rustc` dictionary selects execution triples automatically. A single compiler
label can instead be combined with `exec_triples`. Use `target_triples` to limit
supported target platforms. Omit `rustc` to use the generated compiler while
overriding another component.

Register the custom package instead of the generated Rust compiler toolchains in
`MODULE.bazel`:

```bzl
register_toolchains(
    "//toolchains:all",
    "@default_rust_toolchains//rustfmt:all",
    "@default_rust_toolchains//rust-analyzer:all",
    "@llvm//toolchain:all",
)
```

Keep `@default_rust_toolchains` available through `use_repo` for the Rustfmt and
rust-analyzer registrations above.
Override `rustc_lib`, `rust_doc`, `cargo`, `clippy_driver`, `cargo_clippy`,
`rust_objcopy`, `rust_lld`, `bpf_linker`, or `rust_std` when necessary.

</details>

<details>
<summary>Use a custom host Cargo without downloading Rust toolchains</summary>

This can be useful with the Ferrocene toolchain: dependency resolution can use
its Cargo executable without downloading the default Rust toolchain.

Configure Cargo for dependency resolution in the root `MODULE.bazel`:

```bzl
toolchains = use_extension("@rules_rs//rs/toolchains:module_extension.bzl", "toolchains")
toolchains.host_cargo(
    linux_amd64 = "//toolchain/linux_amd64:bin/cargo",
    linux_arm64 = "//toolchain/linux_arm64:bin/cargo",
    macos_amd64 = "//toolchain/macos_amd64:bin/cargo",
    macos_arm64 = "//toolchain/macos_arm64:bin/cargo",
    windows_amd64 = "//toolchain/windows_amd64:bin/cargo.exe",
    windows_arm64 = "//toolchain/windows_arm64:bin/cargo.exe",
)

register_toolchains("@our_toolchains//...")
```

Provide the attributes for the hosts you use; the other attributes can be omitted.
Cargo is selected for the operating system and architecture of the machine
running Bazel, independently of the build target or remote execution platform.
`amd64` covers x86-64, and `arm64` covers AArch64. If the current host's attribute
is missing, repository setup fails with an error identifying it.

Each label must refer to an existing executable file, not a build target.
Labels in external repositories are also supported. Only the root module's
`host_cargo` tag is used; dependency modules' tags are ignored.

When no module declares `toolchains.toolchain`, custom host Cargo disables the
implicit default Rust toolchain and its downloads. Explicit `toolchains.toolchain`
and `toolchains.experimental_miri` declarations still provision their requested
toolchains. Without `host_cargo`, the default behavior is unchanged.

When the implicit default is disabled, `default_rust_toolchains` is not created.
Supply all required compiler components when using `declare_rustc_toolchains`
with fully custom toolchains.

</details>

<details>
<summary>Reference targets added by <code>crate.annotation</code></summary>

Label attributes in `crate.annotation` are resolved in `MODULE.bazel`, so a relative label does not refer to the generated crate package. Use `extra_aliased_targets` to expose a public target from the generated crate package under an explicit name in the hub repository, then use that hub label. The repository name is the `name` passed to `crate.from_cargo`.

See [`3rd_party/apriltag-sys/include.MODULE.bazel`](3rd_party/apriltag-sys/include.MODULE.bazel) for an example.

</details>

<details>
<summary>Use legacy rules_rust toolchains or platforms</summary>

You can keep an existing `rules_rust` toolchain setup during migration. In that mode, configure toolchains from `@rules_rust` and tell `crate.from_cargo(...)` to render selects against legacy `rules_rust` platform labels.

```bzl
rules_rust = use_extension("@rules_rs//rs:rules_rust.bzl", "rules_rust")
use_repo(rules_rust, "rules_rust")

rust = use_extension("@rules_rs//rs:rules_rust_reexported_extensions.bzl", "rust")
rust.toolchain(
    edition = "2024",
    versions = ["1.92.0"],
)

use_repo(rust, "rust_toolchains")
register_toolchains("@rust_toolchains//:all")

crate = use_extension("@rules_rs//rs:extensions.bzl", "crate")
crate.from_cargo(
    name = "crates",
    cargo_lock = "//:Cargo.lock",
    cargo_toml = "//:Cargo.toml",
    platform_triples = [
        "x86_64-unknown-linux-gnu",
    ],
    use_legacy_rules_rust_platforms = True,
)
use_repo(crate, "crates")
```

</details>

<details>
<summary>Cross ABI target details</summary>

Proc macros and build scripts run in the exec configuration, while your library or binary may be built for a different target ABI. Include both exec and target triples when they differ.

Windows GNULVM target with MSVC exec:

```bzl
platform_triples = [
    "x86_64-pc-windows-msvc",     # exec
    "x86_64-pc-windows-gnullvm",  # target
]
```

Linux musl target with GNU exec:

```bzl
platform_triples = [
    "x86_64-unknown-linux-gnu",   # exec
    "x86_64-unknown-linux-musl",  # target
]
```

The default Windows exec toolchain is MSVC-flavored. The upstream GNULVM toolchain dynamically links `libunwind`, which may not exist on a stock Windows machine.

The Linux exec toolchains are GNU-flavored. When targeting musl, also include the corresponding GNU triple for build scripts and proc macros.

ARM soft-float (`*eabi`) and hard-float (`*eabihf`) triples — and the `aarch64-unknown-none` / `aarch64-unknown-none-softfloat` pair — share the same CPU and OS constraints, so they are disambiguated by an explicit float-ABI constraint. Bare ARM platforms default to **hard-float** (`@rules_rs//rs/platforms/constraints:hardfloat`), the conventional Linux ARM ABI (armhf) and bare-metal default. To target a soft-float triple from a custom platform, add `@rules_rs//rs/platforms/constraints:softfloat` to its `constraint_values`. The `rules_rs`-published platforms (e.g. `@rules_rs//rs/platforms:arm-unknown-linux-musleabi`) already carry the correct value.

Similarly, `wasm32-wasip1` and `wasm32-wasip1-threads` are disambiguated by a WebAssembly threads constraint that defaults to threads-off (`@rules_rs//rs/platforms/constraints:wasm_threads_off`); the threaded variant opts in with `@rules_rs//rs/platforms/constraints:wasm_threads_on`.

</details>

<details>
<summary>Remote execution platforms</summary>

Remote execution platforms can inherit from a triple-based platform published by `rules_rs`, then add execution properties:

```bzl
platform(
    name = "rbe_linux_amd64_gnu",
    parents = ["@rules_rs//rs/platforms:x86_64-unknown-linux-gnu"],
    exec_properties = {
        "container-image": "docker://ghcr.io/example/rbe-linux-gnu:latest",
    },
)
```

Keep host ABI constraints aligned with your exec toolchain choice. Model target ABI differences with target platforms and `platform_triples`.

</details>

<details>
<summary>Patch or override rules_rust</summary>

`rules_rs` exports a `rules_rust` module extension that provisions the pinned, patched `rules_rust` repository:

```bzl
rules_rust = use_extension("@rules_rs//rs:rules_rust.bzl", "rules_rust")

rules_rust.patch(
    patches = ["//:my_rules_rust_fix.patch"],
    strip = 1,
)

use_repo(rules_rust, "rules_rust")
```

If you need to replace the pinned repository completely, use `override_repo`:

```bzl
bazel_dep(name = "rules_rs", version = "0.0.33")
bazel_dep(name = "rules_rust", version = "0.68.1")

archive_override(
    module_name = "rules_rust",
    integrity = "sha256-...",
    strip_prefix = "rules_rust-<commit>",
    urls = ["https://github.com/my-org/rules_rust/archive/<commit>.tar.gz"],
)

rules_rust_ext = use_extension("@rules_rs//rs:rules_rust.bzl", "rules_rust")
override_repo(rules_rust_ext, rules_rust = "rules_rust")
```

Overriding with a version that does not include required patches from [hermeticbuild/rules_rust](https://github.com/hermeticbuild/rules_rust) may cause build failures.

</details>

<details>
<summary>Protobuf with prost</summary>

Load prost rules and default toolchains from the reexported `@rules_rust` repository:

```bzl
load("@rules_rust//extensions/prost:defs.bzl", "rust_prost_library")
```

```bzl
bazel_dep(name = "rules_proto", version = "7.1.0")
bazel_dep(name = "protobuf", version = "34.0.bcr.1")

register_toolchains(
    "@rules_rust//extensions/prost:default_prost_toolchain",
    "@//path/to/proto_toolchain",
)
```

If you need different prost, tonic, or plugin versions, define your own `rust_prost_toolchain` from `@rules_rust//extensions/prost:defs.bzl`.

`rules_rs` also exposes a `@rules_rust_prost` compatibility repository to ease migration of existing code:

```bzl
rules_rust_prost = use_extension("//rs:rules_rust_prost.bzl", "rules_rust_prost")
use_repo(rules_rust_prost, "rules_rust_prost")
```

</details>

<details>
<summary>Python extensions with PyO3</summary>

Load PyO3 rules and default toolchains from the reexported `@rules_rust` repository:

```bzl
load("@rules_rust//extensions/pyo3:defs.bzl", "pyo3_extension")
```

```bzl
register_toolchains(
    "@rules_rust//extensions/pyo3/toolchains:toolchain",
    "@rules_rust//extensions/pyo3/toolchains:rust_toolchain",
)
```

If you need different PyO3 versions or Python discovery behavior, define your own `pyo3_toolchain` or `rust_pyo3_toolchain` from `@rules_rust//extensions/pyo3:defs.bzl`. 

`rules_rs` also exposes a `@rules_rust_pyo3` compatibility repository to ease migration of existing cod:

```bzl
rules_rust_pyo3 = use_extension("//rs:rules_rust_pyo3.bzl", "rules_rust_pyo3")
use_repo(rules_rust_pyo3, "rules_rust_pyo3")
```

</details>

<details>
<summary>Dependency resolution caveats</summary>

`rules_rs` currently supports Cargo lockfile based resolution through `crate.from_cargo(...)`.
`crate.spec` and vendoring mode are not currently supported.

Normal dependencies and build dependencies resolve features separately for each
`platform_triples` entry. Each generated crate has one library target, with
features and dependencies selected by Cargo resolution. A crate shares the
default Bazel configuration only when its features and dependencies are
independent of the original Cargo target and every normal and build dependency
can also share. Otherwise, it retains the original target triple.
Crates unreachable from the Cargo roots on every configured platform are
incompatible. Generating a Bazel label does not make the crate an additional
Cargo root.

Build scripts with identical features, dependencies, and `cargo_target_triple` share
one target. The build script is selected in the target configuration before
its dependencies transition to the execution platform.

For first-party build scripts, load `cargo_build_script` from the generated
Cargo repository's `defs.bzl`. It selects the package's Cargo features, build
dependencies, and aliases before the execution transition:

```bzl
load("@crates//:defs.bzl", "cargo_build_script")

cargo_build_script(
    name = "build_script",
    srcs = ["build.rs"],
    crate_root = "build.rs",
)
```

Use `all_crate_deps()`, `aliases()`, and `crate_features()` for libraries. The same first-party library target can be
a normal dependency and a build dependency, including when a generated crate
depends back on it. `cargo_target_triple` records the original target triple
for build dependencies and survives execution transitions. Generated crates
clear this setting when they share the default configuration. Rust toolchains
clear it. C++ toolchains retain their existing configuration.
`all_crate_deps(build = True)` is available only when build dependencies need
the current Cargo resolution and their labels are identical across target
platforms. Otherwise, use the generated `cargo_build_script`; the raw
`@rules_rs//rs:cargo_build_script.bzl` rule cannot establish that resolution
from a dependency list. `aliases(build = True)` requires identical alias maps.
Annotation-added dependencies retain the labels supplied by the user. Registry
crates using `package.metadata.bazel.deps` must also declare those dependencies
with `crate.annotation(deps = ...)` when configurations would otherwise be
shared. Cargo registry metadata does not include these Bazel dependencies.

Proc macros reached through normal dependencies use the existing conservative
target feature resolution for their normal dependencies. They do not share
features enabled only through build dependencies, so they may need explicit
`crate_features` annotations.
For example, a PyO3 toolchain that sets `PYO3_NO_PYTHON` needs its chosen
`abi3-py3*` feature on `pyo3-build-config` in both resolutions.

`gen_binaries` resolves requested binaries for the target platform. For a package
used only by build dependencies, it enables default features and `crate_features`
annotations. If normal dependencies already reach the package, it preserves
those resolved features, including `default-features = false`. Build-only feature requests do not enable
features on generated binaries.

Cargo workspaces sometimes use a self-referencing dev-dependency to enable extra features for tests:

```toml
[dev-dependencies]
mycrate = { path = ".", features = ["test-utils"] }
```

`rules_rs` suppresses the generated self-edge in `aliases()` and `all_crate_deps()` so this pattern does not create a Bazel dependency cycle. The requested features are still part of workspace feature resolution, so they may be enabled on the first-party crate more broadly than Cargo would enable them for a single targeted test command.

If you need separate normal and test feature variants, model them as separate Bazel targets, with the test-only variant setting extra `crate_features` and `testonly = True`.

</details>

<details>
<summary>Migration from rules_rust loads</summary>

If you import `rules_rust` through the `rules_rs` extension, existing `load("@rules_rust//...")` statements can be kept during migration.

For long-term hygiene, prefer migrating common Rust rule loads to `@rules_rs//rs:*` wrappers. A helper script is provided:

```bash
./scripts/rewrite_rules_rust_loads.sh
```

The script rewrites common `@rules_rust` Rust loads to `@rules_rs//rs:*` wrappers and then formats with `buildifier`.

</details>

## Public API

See https://registry.bazel.build/modules/rules_rs/latest/docs

## Users

- [OpenAI Codex](https://github.com/openai/codex)
- [Aspect CLI](https://github.com/aspect-build/aspect-cli)
- [Astradot](https://astradot.com)
- [Datadog Agent](https://github.com/DataDog/datadog-agent)
- [ZML](https://github.com/zml/zml/tree/zml/v2)
- [rules_py](https://github.com/aspect-build/rules_py)
- [JetBrains](https://github.com/JetBrains/intellij-community), used in closed sources of [JetBrains Air](https://air.dev/)
- [Perplexity](https://perplexity.ai)
- [formatjs](https://github.com/formatjs/formatjs)
- [Trace Machina Nativelink](https://github.com/TraceMachina/nativelink)
- [Selenium](https://github.com/SeleniumHQ/selenium)
- [Etsy](https://www.etsy.com/)
- [Aya](https://github.com/aya-rs/aya) and [bpf-linker](https://github.com/aya-rs/bpf-linker)
- [Xybrid](https://github.com/xybrid-ai/xybrid)
- [Drake](https://github.com/RobotLocomotion/drake)

### Cargo feature roots and procedural macros

Feature resolution uses the manifest's library kind and crate name. Procedural
macros and their normal dependencies resolve for the execution platform, separately
from target libraries, even when the host and target triples are equal. Registry
manifests are read from checksum-verified archives; Git manifest facts are refreshed
when upgrading from facts that did not include the library kind.

`crate.from_cargo` defaults to Cargo's workspace default members. Set `packages`
to select different root packages, `features` to add features by selected package
name, and `default_features = False` to disable root default features. Development
dependencies are included for selected roots by default; `include_dev = False`
resolves a build-only closure. Development dependencies of transitive crates are
excluded from execution copies.

The resolver tests include unit graphs captured from Cargo for native and cross
builds, a renamed procedural macro, shared host/target dependencies and selected
macro roots. `rs/private/feature_context_fixture/refresh.py` refreshes that oracle
using a nightly Cargo toolchain without compiling the fixture.

### Restricting crate visibility

Use `crate.visibility` in `MODULE.bazel` to restrict direct use of selected Cargo
packages. Names match exactly, or by prefix with a trailing `*`. Settings apply to
all hubs declared by the same module unless `repositories` selects specific hubs.
Overlapping settings for a crate are rejected.

```starlark
crate.visibility(
    crates = ["tauri", "tauri-*"],
    repositories = ["crates"],
    visibility = ["//apps:__subpackages__"],
)
```

The setting covers generated libraries, procedural macros, binaries, and hub
aliases, including versioned aliases. Labels resolve in the declaring module.
Unconfigured crates remain public. Generated crates in the same Cargo closure
retain access to each other so transitive dependencies still build. Package
metadata remains public for metadata collectors. Empty or private visibility
prevents direct workspace use while retaining that internal dependency access.

### Cargo license metadata

Generated `*_package_metadata` targets include the manifest's complete `license`
expression and declared `license-file` contents. Expressions such as
`MIT OR Apache-2.0` are retained verbatim; generation does not choose an alternative.
A file-only license uses the identifier `NOASSERTION`. A package declaring neither
field has no license attribute.

Registry crates and Git workspace members use the same metadata. Workspace
inheritance resolves license paths relative to the workspace manifest. Declared
license files must exist inside the source repository; their contents are copied
beside each crate so Bazel package boundaries cannot hide them from collectors.
