load("@bazel_lib//lib:repo_utils.bzl", "repo_utils")
load("@bazel_skylib//lib:paths.bzl", "paths")
load("@host_cargo//:defs.bzl", "RS_HOST_CARGO_LABEL")
load("//rs/platforms:triples.bzl", "SUPPORTED_EXEC_TRIPLES")
load("//rs/private:annotations.bzl", "annotation_for", "build_annotation_map", "well_known_annotation_snippet_paths")
load("//rs/private:cargo_credentials.bzl", "load_cargo_credentials", "registry_auth_headers")
load(
    "//rs/private:cargo_workspace_graph.bzl",
    "cargo_toml_fact",
    "locked_packages",
    "render_dep_data",
    "resolve_cargo_workspace_members",
    "resolve_packages",
    "split_lockfile_packages",
    "workspace_dep_data",
    _fq_crate = "fq_crate",
    _manifest_package_dir = "manifest_package_dir",
    _normalize_path = "normalize_path",
)
load("//rs/private:crate_configurations.bzl", "prepare_crate_configurations")
load("//rs/private:crate_repository.bzl", "crate_repository", "local_crate_repository")
load("//rs/private:downloader.bzl", "download_metadata_for_git_crates", "git_crate_strip_prefix", "git_manifest_fact_key", "new_downloader_state", "parse_git_url", "start_crate_registry_downloads", "start_github_downloads")
load("//rs/private:git_cargo_workspace_repository.bzl", "git_cargo_workspace_repository")
load("//rs/private:git_crate_metadata_repository.bzl", "git_crate_metadata_repository")
load("//rs/private:lint_flags.bzl", "cargo_toml_lint_flags", "workspace_cargo_toml_lint_flags")
load("//rs/private:registry_config_repository.bzl", "registry_config_repository")
load("//rs/private:registry_utils.bzl", "CRATES_IO_REGISTRY", "registry_config_repo_name", "registry_download_url", "resolve_registry_source")
load("//rs/private:repository_utils.bzl", "render_select")
load("//rs/private:select_utils.bzl", "platform_label")
load("//rs/private:toml2json.bzl", "run_toml2json")
load("//rs/private:visibility.bzl", "visibility_for")
load("//rs/private:workspace_index.bzl", "workspace_index")

def _spoke_repo(hub_name, name, version):
    s = "%s__%s-%s" % (hub_name, name, version)
    if "+" in s:
        s = s.replace("+", "-")
    return s

def _git_repo_remote_name(remote):
    scheme_separator = remote.find("://")
    if scheme_separator != -1:
        remote = remote[scheme_separator + len("://"):]

    name = remote.replace("/", "_").replace(":", "_").replace("@", "_")

    # The Git repository map checks the full remote and commit for collisions.
    return name if len(name) <= 100 else name[:80] + "_" + str(hash(remote))

def _external_repo_for_git_source(hub_name, remote, commit):
    return hub_name + "__" + _git_repo_remote_name(remote) + "_" + commit[:8]

def _git_crate_purl(name, version, remote, commit):
    return "pkg:cargo/%s@%s?vcs_url=git+%s@%s" % (name, version, remote, commit)

def _render_ordered_string_list(items):
    """Like _render_string_list but preserves insertion order."""
    return ",\n        ".join([repr(item) for item in items])

def _render_cargo_lints_target(name, lint_flags):
    return """
cargo_lints(
    name = {name},
    rustc_lint_flags = [
        {rustc}
    ],
    clippy_lint_flags = [
        {clippy}
    ],
    rustdoc_lint_flags = [
        {rustdoc}
    ],
)""".format(
        name = repr(name),
        rustc = _render_ordered_string_list(lint_flags.rustc_lint_flags),
        clippy = _render_ordered_string_list(lint_flags.clippy_lint_flags),
        rustdoc = _render_ordered_string_list(lint_flags.rustdoc_lint_flags),
    )

def _date(ctx, label):
    return
    result = ctx.execute(["gdate", '+"%Y-%m-%d %H:%M:%S.%3N"'])
    print(label, result.stdout)

def _label_directory(label):
    idx = label.name.rfind("/")
    if idx == -1:
        return label.package

    return paths.join(label.package, label.name[:idx])

def _git_crate_package_path(annotation, strip_prefix):
    workspace_dir = annotation.workspace_cargo_toml.removesuffix("Cargo.toml").removesuffix("/")
    crate_dir = (strip_prefix or "").removeprefix("./").removesuffix("/")

    if workspace_dir and crate_dir:
        return _normalize_path(paths.normalize(paths.join(workspace_dir, crate_dir)))
    if workspace_dir:
        return _normalize_path(workspace_dir)
    return _normalize_path(crate_dir)

def _target_label(repo_name, package_path, target):
    if package_path:
        return "@%s//%s:%s" % (repo_name, package_path, target)
    return "@%s//:%s" % (repo_name, target)

def _additive_build_file_content(mctx, annotation):
    content = ""
    if annotation.additive_build_file:
        content += mctx.read(annotation.additive_build_file)
    content += annotation.additive_build_file_content
    return content

def _generate_hub_and_spokes(
        mctx,
        hub_name,
        annotations,
        suggested_annotation_snippet_paths,
        cargo_path,
        cargo_lock_path,
        workspace_cargo_toml_json,
        all_packages,
        platform_triples,
        cargo_credentials,
        cargo_config,
        validate_lockfile,
        debug,
        generate_lint_config,
        use_legacy_rules_rust_platforms,
        target_hosts = {},
        workspace_library_target = "",
        root_packages = [],
        root_features = {},
        include_dev = True,
        default_features = True,
        visibilities = [],
        dry_run = False):
    """Generates repositories for the transitive closure of the Cargo workspace.

    Args:
        mctx (module_ctx): The module context object.
        hub_name (string): name
        annotations (dict): Annotation tags to apply.
        suggested_annotation_snippet_paths (dict): Mapping crate -> snippet file path.
        cargo_path (path): Path to hermetic `cargo` binary.
        cargo_lock_path (path): Cargo.lock path
        workspace_cargo_toml_json (dict): Parsed workspace Cargo.toml
        all_packages: list[package]: from cargo lock parsing
        platform_triples (list[string]): Triples to resolve for
        cargo_credentials (dict): Mapping of registry to auth token.
        cargo_config (label): .cargo/config.toml file
        validate_lockfile (bool): If true, validate we have appropriate versions in Cargo.lock
        debug (bool): Enable debug logging
        generate_lint_config (bool): Generate per-package Cargo lint configuration.
        visibilities: Visibility tags from the declaring module.
        dry_run (bool): Run all computations but do not create repos. Useful for benchmarking.
    """
    _date(mctx, "start")

    mctx.report_progress("Reading workspace metadata")
    result = mctx.execute(
        [cargo_path, "metadata", "--no-deps", "--locked", "--format-version=1", "--quiet"] +
        (["--config", str(mctx.path(cargo_config))] if cargo_config else []),
        working_directory = str(mctx.path(cargo_lock_path).dirname),
    )
    if result.return_code != 0:
        fail(result.stdout + "\n" + result.stderr)
    cargo_metadata = json.decode(result.stdout)

    _date(mctx, "parsed cargo metadata")

    existing_facts = getattr(mctx, "facts", {}) or {}
    facts = {}

    split_packages = split_lockfile_packages(
        hub_name,
        cargo_metadata,
        workspace_cargo_toml_json,
        all_packages,
    )
    packages = split_packages.packages
    workspace_members = split_packages.workspace_members

    mctx.report_progress("Computing dependencies and features")

    facts_by_fq_crate = {}
    registry_configs = {}
    for package in packages:
        name = package["name"]
        version = package["version"]
        source = package["source"]
        mctx.report_progress("Reading locked package " + name + " " + version)

        if source.startswith("sparse+"):
            key = name + "_" + version
            fact = existing_facts.get(key)
            if fact:
                facts[key] = fact
                fact = json.decode(fact)
            else:
                package["download_token"].wait()

                # TODO(zbarsky): Should we also dedupe this parsing?
                for line in mctx.read(name + ".jsonl").strip().split("\n"):
                    if version not in line:
                        continue
                    metadata = json.decode(line)
                    if metadata["vers"] != version:
                        continue

                    features = metadata.get("features") or {}

                    # Crates published with newer Cargo populate this field for `resolver = "2"`.
                    # It can express more nuanced feature dependencies and overrides the keys from legacy features, if present.
                    features.update(metadata.get("features2") or {})

                    dependencies = metadata["deps"]

                    for dep in dependencies:
                        if dep["default_features"]:
                            dep.pop("default_features")
                        if not dep["features"]:
                            dep.pop("features")
                        if dep.get("target", "") == None:
                            dep.pop("target")
                        if dep["kind"] == "normal":
                            dep.pop("kind")
                        if not dep["optional"]:
                            dep.pop("optional")

                    fact = dict(
                        features = features,
                        dependencies = dependencies,
                    )

                    # Nest a serialized JSON since max path depth is 5.
                    facts[key] = json.encode(fact)
                    break

                if fact == None:
                    fail("Sparse registry %s has no metadata for %s %s" % (source, name, version))
        elif source.startswith("path+"):
            # Always re-read a path dependency's Cargo.toml instead of using cached facts.
            # Path dependencies are local, and Cargo.toml can change features or
            # dependencies without changing Cargo.lock, causing stale resolution.
            # Do not return path dependency facts for storage in MODULE.bazel.lock.
            # Watch Cargo.toml so Bazel re-runs the extension when Cargo.toml changes.
            cargo_toml_path = paths.join(package["local_path"], "Cargo.toml")
            mctx.watch(mctx.path(cargo_toml_path))
            cargo_toml_json = run_toml2json(mctx, cargo_toml_path)
            fact = cargo_toml_fact(cargo_toml_json, {})
        elif source.startswith("git+"):
            key = git_manifest_fact_key(source, name)
            fact = existing_facts.get(key)
            if fact:
                facts[key] = fact
                fact = json.decode(fact)
            else:
                info = package.get("member_crate_cargo_toml_info")
                if info:
                    # TODO(zbarsky): These tokens got enqueues last, so this can bottleneck
                    # We can try a bit harder to interleave things if we care.
                    info.token.wait()
                    package_workspace_cargo_toml_json = package["workspace_cargo_toml_json"]
                    cargo_toml_json = run_toml2json(mctx, info.path)
                else:
                    cargo_toml_json = package["cargo_toml_json"]
                    package_workspace_cargo_toml_json = package.get("workspace_cargo_toml_json")
                strip_prefix = package.get("strip_prefix", "")

                fact = cargo_toml_fact(cargo_toml_json, package_workspace_cargo_toml_json, strip_prefix = strip_prefix)

                if not fact["dependencies"] and debug:
                    print(name, version, package["source"])

                # Nest a serialized JSON since max path depth is 5.
                facts[key] = json.encode(fact)

            package["strip_prefix"] = fact["strip_prefix"]
        else:
            fail("Unknown source %s for crate %s" % (source, name))

        # Sparse indexes omit the library kind and authored crate name. Read
        # the checksum-verified manifest once, then retain these facts in the lockfile.
        if source.startswith("sparse+") and "proc_macro" not in fact:
            headers = registry_auth_headers(cargo_credentials, source)
            registry_config = registry_configs.get(source)
            if registry_config == None:
                config_path = "registry_config_" + str(len(registry_configs)) + ".json"
                mctx.download(source.removeprefix("sparse+") + "config.json", config_path, headers = headers)
                registry_config = json.decode(mctx.read(config_path))
                registry_configs[source] = registry_config
            destination = "manifests/" + hub_name + "/" + name + "-" + version
            mctx.download_and_extract(
                url = registry_download_url(registry_config, name, version, package["checksum"]),
                output = destination,
                type = "tar.gz",
                stripPrefix = name + "-" + version,
                sha256 = package["checksum"],
                headers = headers,
            )
            manifest = run_toml2json(mctx, destination + "/Cargo.toml")
            manifest_fact = cargo_toml_fact(manifest)
            fact["proc_macro"] = manifest_fact["proc_macro"]
            fact["crate_name"] = manifest_fact["crate_name"]
            facts[name + "_" + version] = json.encode(fact)
        facts_by_fq_crate[_fq_crate(name, version)] = fact

    resolved_facts = resolve_packages(packages, facts_by_fq_crate, platform_triples)
    feature_resolutions_by_fq_crate = resolved_facts.feature_resolutions_by_fq_crate
    versions_by_name = resolved_facts.versions_by_name

    # Only files in the current Bazel workspace can/should be watched, so check where our manifests are located.
    watch_manifests = cargo_lock_path.repo_name == ""

    workspace_resolution = resolve_cargo_workspace_members(
        mctx,
        cargo_metadata = cargo_metadata,
        packages = packages,
        workspace_members = workspace_members,
        versions_by_name = versions_by_name,
        feature_resolutions_by_fq_crate = feature_resolutions_by_fq_crate,
        annotations = annotations,
        platform_triples = platform_triples,
        materialize_workspace_members = False,
        exec_platform_triples = SUPPORTED_EXEC_TRIPLES,
        validate_lockfile = validate_lockfile,
        debug = debug,
        dep_label_prefix = "@%s//:" % hub_name,
        watch_manifests = watch_manifests,
        root_packages = root_packages,
        features = root_features,
        include_dev = include_dev,
        default_features = default_features,
    )
    cfg_match_cache = workspace_resolution.cfg_match_cache
    platform_cfg_attrs = workspace_resolution.platform_cfg_attrs
    workspace_dep_labels_by_triple = workspace_resolution.workspace_dep_labels_by_triple
    workspace_dep_versions_by_name = workspace_resolution.workspace_dep_versions_by_name

    _date(mctx, "set up initial deps!")

    package_by_fq = {
        _fq_crate(package["name"], package["version"]): package
        for package in packages
    }
    workspace_crates = [fq for fq in feature_resolutions_by_fq_crate if fq not in package_by_fq]
    preserve_cargo_target_triple = []
    for fq, package in package_by_fq.items():
        annotation = annotation_for(annotations, package["name"], package["version"], hub_name)
        fact = facts_by_fq_crate[fq]
        opaque_deps = bool(fact.get("bazel_deps")) or (package["source"].startswith("git+") and ("bazel_deps" not in fact or annotation.patches))
        for field in ["deps", "link_deps", "data", "build_script_data", "build_script_data_select", "build_script_tools", "build_script_tools_select", "build_script_env_files", "build_script_toolchains"]:
            if getattr(annotation, field):
                opaque_deps = True
                break
        if opaque_deps:
            preserve_cargo_target_triple.append(fq)
    configurations_by_crate = prepare_crate_configurations(
        feature_resolutions_by_fq_crate,
        workspace_resolution.exec_resolutions_by_cargo_target_triple,
        dep_label_prefix = "@%s//:" % hub_name,
        exec_platform_triples = SUPPORTED_EXEC_TRIPLES,
        preserve_cargo_target_triple = preserve_cargo_target_triple,
        workspace_crates = workspace_crates,
    )

    proc_macro_labels = ["@%s//:%s" % (hub_name, fq) for fq, fact in facts_by_fq_crate.items() if fact.get("proc_macro")]

    mctx.report_progress("Initializing spokes")

    use_home_cargo_credentials = bool(cargo_credentials)

    # Generated crates must remain able to depend on each other even when their
    # use from the consuming workspace is restricted. Include the real Git
    # checkout repositories, not just their metadata spokes.
    internal_packages = {"@%s//:__pkg__" % hub_name: True}
    for package in packages:
        source = package["source"]
        if source.startswith("git+"):
            remote, commit = parse_git_url(source)
            repo_name = _external_repo_for_git_source(hub_name, remote, commit)
        else:
            repo_name = _spoke_repo(hub_name, package["name"], package["version"])
        label = "@%s//:__subpackages__" % repo_name
        internal_packages[label] = True
    internal_packages = internal_packages.keys()

    for package in packages:
        crate_name = package["name"]
        version = package["version"]
        source = package["source"]

        annotation = annotation_for(annotations, crate_name, version, hub_name)
        if annotation.source and annotation.source != package.get("lock_source"):
            fail("Source annotation does not match locked package %s %s: expected %s, got %s" % (crate_name, version, annotation.source, package.get("lock_source")))
        suggested_annotation = None
        if annotation.gen_build_script == "auto":
            snippet_path = suggested_annotation_snippet_paths.get(crate_name)
            if snippet_path:
                suggested_annotation = mctx.read(snippet_path).strip()

        if suggested_annotation:
            print("""
WARNING: A well-known crate annotation exists to make builds of {crate} more hermetic! Apply the following to your MODULE.bazel:

```
{formatted_well_known_annotation}
```

If non-hermetic builds of {crate} are acceptable, then you can disable this warning by configuring your MODULE.bazel like so:

```
crate.annotation(
    crate = "{crate}",
    gen_build_script = "on",
)
```""".format(
                crate = crate_name,
                formatted_well_known_annotation = suggested_annotation,
            ))

        crate_configurations = configurations_by_crate[_fq_crate(crate_name, version)]
        kwargs = dict(
            hub_name = hub_name,
            crate_visibility = visibility_for(visibilities, hub_name, crate_name, internal_packages),
            proc_macro_labels = proc_macro_labels,
            gen_build_script = annotation.gen_build_script,
            cargo_target_triple_map = crate_configurations.cargo_target_triple_map,
            configurations = json.encode(crate_configurations.configurations),
            build_script_data = annotation.build_script_data,
            build_script_data_select = annotation.build_script_data_select,
            build_script_env = annotation.build_script_env,
            build_script_env_files = annotation.build_script_env_files,
            allow_build_script_to_detect_nonhermetic_paths = annotation.allow_build_script_to_detect_nonhermetic_paths,
            build_script_toolchains = annotation.build_script_toolchains,
            build_script_tools = annotation.build_script_tools,
            build_script_tags = annotation.build_script_tags,
            build_script_tools_select = annotation.build_script_tools_select,
            build_script_env_select = annotation.build_script_env_select,
            rustc_env = annotation.rustc_env,
            rustc_flags = annotation.rustc_flags,
            rustc_flags_select = annotation.rustc_flags_select,
            data = annotation.data,
            deps = annotation.deps,
            crate_tags = annotation.tags,
            link_deps = annotation.link_deps,
            use_legacy_rules_rust_platforms = use_legacy_rules_rust_platforms,
        )

        repo_name = _spoke_repo(hub_name, crate_name, version)
        package["target_repo_name"] = repo_name
        package["target_package_path"] = ""

        if source.startswith("sparse+"):
            checksum = package["checksum"]

            if dry_run:
                continue

            qualifiers = {}
            if source != CRATES_IO_REGISTRY:
                qualifiers["repository_url"] = source.split("+", 1)[1]

            crate_repository(
                name = repo_name,
                additive_build_file = annotation.additive_build_file,
                additive_build_file_content = annotation.additive_build_file_content,
                crate_name = crate_name,
                version = version,
                registry_config = "@%s//:dl" % registry_config_repo_name(hub_name, source),
                sbom_extra_qualifiers = qualifiers,
                checksum = checksum,
                gen_binaries = annotation.gen_binaries,
                patch_args = annotation.patch_args,
                patch_tool = annotation.patch_tool,
                patches = annotation.patches,
                # The repository will need to recompute these, but this lets us avoid serializing them.
                use_home_cargo_credentials = use_home_cargo_credentials,
                cargo_config = cargo_config,
                source = source,
                **kwargs
            )
        elif source.startswith("path+"):
            if dry_run:
                continue

            # TODO What PURL should that be ?
            local_crate_repository(
                name = repo_name,
                additive_build_file = annotation.additive_build_file,
                additive_build_file_content = annotation.additive_build_file_content,
                gen_binaries = annotation.gen_binaries,
                patch_args = annotation.patch_args,
                patch_tool = annotation.patch_tool,
                patches = annotation.patches,
                path = package["local_path"],
                **kwargs
            )
        elif source.startswith("git+"):
            remote, commit = parse_git_url(source)

            package_path = _git_crate_package_path(annotation, package.get("strip_prefix"))
            package["target_repo_name"] = _external_repo_for_git_source(hub_name, remote, commit)
            package["target_package_path"] = package_path

            if dry_run:
                continue

            git_crate_metadata_repository(
                name = repo_name,
                package_name = crate_name,
                package_version = version,
                purl = _git_crate_purl(crate_name, version, remote, commit),
                **kwargs
            )
        else:
            fail("Unknown source %s for crate %s" % (source, crate_name))

    _date(mctx, "created repos")

    mctx.report_progress("Initializing hub")

    repo_root = _normalize_path(cargo_metadata["workspace_root"])
    workspace_package = _label_directory(cargo_lock_path)

    workspace_lints_present = generate_lint_config and "lints" in workspace_cargo_toml_json.get("workspace", {})
    workspace_manifest_path = paths.join(repo_root, "Cargo.toml")
    lint_configs = {}
    member_lint_configs = {}
    package_lint_targets = []
    lint_packages = cargo_metadata["packages"] if generate_lint_config else []
    for index, package in enumerate(lint_packages):
        manifest_path = _normalize_path(package["manifest_path"])
        if manifest_path == workspace_manifest_path:
            cargo_toml_json = workspace_cargo_toml_json
        else:
            cargo_toml_json = run_toml2json(mctx, package["manifest_path"])
        lints = cargo_toml_json.get("lints", {})
        package_dir = _manifest_package_dir(manifest_path, repo_root)
        bazel_package = paths.join(workspace_package, package_dir) if package_dir else workspace_package

        if lints.get("workspace") == True:
            if workspace_lints_present:
                lint_configs[bazel_package] = "@%s//:workspace_cargo_lints" % hub_name
                member_lint_configs[package_dir] = lint_configs[bazel_package]
        elif lints.get("rust") or lints.get("clippy") or lints.get("rustdoc"):
            if manifest_path == workspace_manifest_path:
                lint_configs[bazel_package] = "@%s//:cargo_lints" % hub_name
                member_lint_configs[package_dir] = lint_configs[bazel_package]
            else:
                target_name = "_cargo_lints_%d" % index
                lint_configs[bazel_package] = "@%s//:%s" % (hub_name, target_name)
                member_lint_configs[package_dir] = lint_configs[bazel_package]
                package_lint_targets.append((
                    target_name,
                    cargo_toml_lint_flags(cargo_toml_json),
                ))

    cargo_target_triples = set()
    for crate_configurations in configurations_by_crate.values():
        cargo_target_triples.update(crate_configurations.configurations)
    package_metadata = []
    hub_contents = [
        'load("@rules_rs//rs/private:cargo_select.bzl", "cargo_config_settings")',
        'load("@rules_rs//rs/private:proc_macro_alias.bzl", "proc_macro_alias")',
        "cargo_config_settings(%r, %r, %r)" % (sorted(cargo_target_triples), sorted(set(platform_triples + SUPPORTED_EXEC_TRIPLES)), use_legacy_rules_rust_platforms),
    ]
    for name, versions in versions_by_name.items():
        crate_visibility = visibility_for(visibilities, hub_name, name, internal_packages)
        for version in versions:
            annotation = annotation_for(annotations, name, version, hub_name)
            package = package_by_fq[_fq_crate(name, version)]
            target_repo_name = package["target_repo_name"]
            target_package_path = package["target_package_path"]

            hub_contents.append("""
alias(
    name = "{name}-{version}",
    actual = "{actual}",
    visibility = {visibility},
)""".format(visibility = crate_visibility, name = name, version = version, actual = _target_label(target_repo_name, target_package_path, name)))

            # The package's package_metadata, for supply-chain checks.
            hub_contents.append("""
alias(
    name = "{name}-{version}__metadata",
    actual = "{actual}",
)""".format(name = name, version = version, actual = _target_label(target_repo_name, target_package_path, name + "_package_metadata")))
            package_metadata.append("{}-{}__metadata".format(name, version))

            if facts_by_fq_crate[_fq_crate(name, version)].get("proc_macro"):
                hub_contents.append("proc_macro_alias(name = %r, actual = %r, visibility = %r)" % (name + "-" + version + "__alias", ":" + name + "-" + version, crate_visibility))

            for binary in annotation.gen_binaries:
                hub_contents.append("""
alias(
    name = "{name}-{version}__{binary}",
    actual = "{actual}",
    visibility = {visibility},
)""".format(visibility = crate_visibility, name = name, version = version, binary = binary, actual = _target_label(target_repo_name, target_package_path, binary + "__bin")))

            for alias_name, target in sorted(annotation.extra_aliased_targets.items()):
                hub_contents.append("""
alias(
    name = "{alias_name}-{version}",
    actual = "{actual}",
    visibility = {visibility},
)""".format(
                    visibility = crate_visibility,
                    alias_name = alias_name,
                    version = version,
                    actual = _target_label(target_repo_name, target_package_path, target),
                ))

        if len(versions) == 1 and facts_by_fq_crate[_fq_crate(name, versions[0])].get("proc_macro"):
            hub_contents.append("alias(name = %r, actual = %r, visibility = %r)" % (name + "__alias", ":" + name + "-" + versions[0] + "__alias", crate_visibility))
        workspace_versions = workspace_dep_versions_by_name.get(name) or ([_fq_crate(name, versions[0])] if len(versions) == 1 else [])
        if workspace_versions:
            fq = sorted(workspace_versions)[-1]
            default_version = fq[len(name) + 1:]
            annotation = annotation_for(annotations, name, default_version, hub_name)

            hub_contents.append("""
alias(
    name = "{name}",
    actual = ":{fq}",
    visibility = {visibility},
)""".format(visibility = crate_visibility, name = name, fq = fq))

            for binary in annotation.gen_binaries:
                hub_contents.append("""
alias(
    name = "{name}__{binary}",
    actual = ":{fq}__{binary}",
    visibility = {visibility},
)""".format(visibility = crate_visibility, name = name, fq = fq, binary = binary))

        if len(versions) == 1:
            version = versions[0]
            annotation = annotation_for(annotations, name, version, hub_name)
            for alias_name in sorted(annotation.extra_aliased_targets.keys()):
                hub_contents.append("""
alias(
    name = "{alias_name}",
    actual = ":{alias_name}-{version}",
    visibility = {visibility},
)""".format(
                    visibility = crate_visibility,
                    alias_name = alias_name,
                    version = version,
                ))

    for package in cargo_metadata["packages"]:
        package_dir = _manifest_package_dir(package["manifest_path"], repo_root)
        bazel_package = paths.join(workspace_package, package_dir).removesuffix("/")
        if not bazel_package:
            # The alias relies on Bazel's `//foo/bar` -> `//foo/bar:bar`
            # shorthand to pick a target name. When the workspace member is
            # at the bazel workspace root, there's no path component to
            # derive a name from.
            continue
        hub_contents.append("""
alias(
    name = "{name}-{version}",
    actual = "@@//{bazel_package}",
)""".format(
            name = package["name"],
            version = package["version"],
            bazel_package = bazel_package + (":" + workspace_library_target if workspace_library_target else ""),
        ))

    workspace_deps, conditional_workspace_deps = render_select(
        [],
        workspace_dep_labels_by_triple,
        use_legacy_rules_rust_platforms,
    )

    hub_contents.append(
        """
package(
    default_visibility = ["//visibility:public"],
)

filegroup(
    name = "_workspace_deps",
    srcs = [
        %s
    ]%s,
)

# Every locked package's package_metadata. A filegroup forwards no providers,
# so consumers walk its srcs with an aspect.
filegroup(
    name = "__package_metadata",
    srcs = [
        %s
    ],
)""" % (
            ",\n        ".join(['"%s"' % dep for dep in sorted(workspace_deps)]),
            " + " + conditional_workspace_deps if conditional_workspace_deps else "",
            ",\n        ".join(['":%s"' % target for target in package_metadata]),
        ),
    )

    hub_contents.append("""load("@rules_rs//rs/private:cargo_lints.bzl", "cargo_lints")""")

    hub_contents.append(_render_cargo_lints_target(
        "cargo_lints",
        cargo_toml_lint_flags(workspace_cargo_toml_json),
    ))

    if workspace_lints_present:
        hub_contents.append(_render_cargo_lints_target(
            "workspace_cargo_lints",
            workspace_cargo_toml_lint_flags(workspace_cargo_toml_json),
        ))

    for target_name, lint_flags in package_lint_targets:
        hub_contents.append(_render_cargo_lints_target(target_name, lint_flags))

    resolved_platforms = set()
    for triple in platform_triples:
        resolved_platforms.add(platform_label(triple, use_legacy_rules_rust_platforms))

    defs_bzl_contents = \
        """load(":data.bzl", "DEP_DATA")
load("@rules_rs//rs/private:all_crate_deps.bzl", _all_crate_deps = "all_crate_deps", _crate_aliases = "crate_aliases", _crate_features = "crate_features")
load("@rules_rs//rs/private:cargo_build_script_variants.bzl", _cargo_build_script_for_configurations = "cargo_build_script_for_configurations")

def aliases(package_name = None, normal = False, normal_dev = False, build = False):
    dep_data = DEP_DATA.get(package_name or native.package_name())
    if not dep_data:
        return {{}}

    return _crate_aliases(dep_data, normal = normal, normal_dev = normal_dev, build = build, hub_name = {hub_name}, use_legacy_rules_rust_platforms = {use_legacy_rules_rust_platforms})

def crate_features(package_name = None):
    dep_data = DEP_DATA.get(package_name or native.package_name())
    if not dep_data:
        return []
    return _crate_features(dep_data, hub_name = {hub_name}, use_legacy_rules_rust_platforms = {use_legacy_rules_rust_platforms})

def crate_name(package_name = None):
    dep_data = DEP_DATA.get(package_name or native.package_name())
    if not dep_data:
        return None

    return dep_data["crate_name"]

def edition(package_name = None):
    dep_data = DEP_DATA.get(package_name or native.package_name())
    if not dep_data:
        return None

    return dep_data["edition"]

def lint_config(package_name = None):
    dep_data = DEP_DATA.get(package_name or native.package_name())
    if not dep_data:
        return None

    return dep_data.get("lint_config")

def all_crate_deps(
        normal = False,
        normal_dev = False,
        build = False,
        package_name = None,
        cargo_only = False):

    dep_data = DEP_DATA.get(package_name or native.package_name())
    if not dep_data:
        return []

    return _all_crate_deps(
        dep_data,
        normal = normal,
        normal_dev = normal_dev,
        build = build,
        filter_prefix = {this_repo} if cargo_only else None,
        hub_name = {hub_name},
        use_legacy_rules_rust_platforms = {use_legacy_rules_rust_platforms},
    )

def cargo_build_script(name, package_name = None, **kwargs):
    package_name = package_name or native.package_name()
    dep_data = DEP_DATA.get(package_name)
    if dep_data == None:
        fail("No Cargo package found for %r" % package_name)
    kwargs.setdefault("edition", dep_data["edition"])
    _cargo_build_script_for_configurations(
        name = name,
        configurations = dep_data["configurations"],
        preserve_cargo_target_triple = True,
        hub_name = {hub_name},
        use_legacy_rules_rust_platforms = {use_legacy_rules_rust_platforms},
        **kwargs
    )

RESOLVED_PLATFORMS = select({{
    {target_compatible_with},
    "//conditions:default": ["@platforms//:incompatible"],
}})
""".format(
            use_legacy_rules_rust_platforms = repr(use_legacy_rules_rust_platforms),
            target_compatible_with = ",\n    ".join(['"%s": []' % platform for platform in resolved_platforms]),
            this_repo = repr("@" + hub_name + "//:"),
            hub_name = repr(hub_name),
        )

    _date(mctx, "done")

    data_bzl_contents = render_dep_data(workspace_dep_data(
        cargo_metadata = cargo_metadata,
        dep_label_prefix = "@%s//:" % hub_name,
        platform_triples = platform_triples,
        platform_cfg_attrs = platform_cfg_attrs,
        cfg_match_cache = cfg_match_cache,
        repo_root = repo_root,
        workspace_package = workspace_package,
        use_legacy_rules_rust_platforms = use_legacy_rules_rust_platforms,
        lint_configs = lint_configs,
        configurations_by_crate = configurations_by_crate,
    ))

    if dry_run:
        return

    index_contents = {}
    if target_hosts:
        index_contents["index.json"] = json.encode_indent(workspace_index(hub_name, cargo_metadata, packages, facts_by_fq_crate, feature_resolutions_by_fq_crate, workspace_resolution.exec_resolutions_by_cargo_target_triple, target_hosts, member_lint_configs)) + "\n"
        hub_contents.append('exports_files(["index.json"], visibility = ["//visibility:public"])')
    _hub_repo(
        name = hub_name,
        contents = index_contents | {
            "BUILD.bazel": "\n".join(hub_contents),
            "defs.bzl": defs_bzl_contents,
            "data.bzl": data_bzl_contents,
        },
    )

    return facts

def _crate_impl(mctx):
    # TODO(zbarsky): Kick off `cargo` fetch early to mitigate https://github.com/bazelbuild/bazel/issues/26995
    cargo_path = mctx.path(RS_HOST_CARGO_LABEL)

    downloader_state = new_downloader_state()
    suggested_annotation_snippet_paths = well_known_annotation_snippet_paths(mctx)

    global_cargo_config = None
    global_use_home_cargo_credentials = False
    for mod in mctx.modules:
        # A dependency's standalone configuration must not override the root module's configuration.
        if not mod.is_root:
            continue

        if len(mod.tags.config) > 1:
            fail("Only one `crate.config` tag may be declared by the root module")

        if mod.tags.config:
            global_cargo_config = mod.tags.config[0].cargo_config_toml
            global_use_home_cargo_credentials = mod.tags.config[0].use_home_cargo_credentials

    packages_by_hub_name = {}
    cargo_toml_by_hub_name = {}
    cargo_config_by_hub_name = {}
    parsed_cargo_configs = {}
    cargo_credentials_by_hub_name = {}
    annotations_by_hub_name = {}

    for mod in mctx.modules:
        if not mod.tags.from_cargo:
            if mod.tags.config:
                # The root module can configure dependency closures without declaring a closure.
                # Dependency modules that only declare crate.config remain valid when ignored.
                continue
            fail("`.from_cargo` is required. Please update %s" % mod.name)

        for cfg in mod.tags.from_cargo:
            annotations = build_annotation_map(mod, cfg.name, cfg.platform_triples)
            annotations_by_hub_name[cfg.name] = annotations
            mctx.watch(cfg.cargo_lock)
            mctx.watch(cfg.cargo_toml)

            effective_cargo_config = cfg.cargo_config or global_cargo_config
            cargo_config_by_hub_name[cfg.name] = effective_cargo_config

            cargo_config = {}
            if effective_cargo_config:
                cargo_config_key = str(effective_cargo_config)
                cargo_config = parsed_cargo_configs.get(cargo_config_key)
                if cargo_config == None:
                    mctx.watch(effective_cargo_config)
                    cargo_config = run_toml2json(mctx, effective_cargo_config)
                    parsed_cargo_configs[cargo_config_key] = cargo_config

            cargo_toml_by_hub_name[cfg.name] = run_toml2json(mctx, cfg.cargo_toml)
            cargo_lock = run_toml2json(mctx, cfg.cargo_lock)
            parsed_packages = locked_packages(cargo_lock)
            for crate_name, versions in annotations.items():
                for version, annotation in versions.items():
                    if annotation.patches:
                        matches = [p for p in parsed_packages if p["name"] == crate_name and (version == "*" or p["version"] == version)]
                        if len(matches) != 1 or not matches[0].get("source"):
                            fail("Source repair must select one exact external locked package: %s %s" % (crate_name, version))
            for package in parsed_packages:
                package["hub_name"] = cfg.name
                source = resolve_registry_source(package.get("source"), cargo_config)
                if source:
                    package["source"] = source
            packages_by_hub_name[cfg.name] = parsed_packages

            # Process git downloads first because they may require a followup download if the repo is a workspace,
            # so we want to enqueue them early so they don't get delayed by 1-shot registry downloads.
            start_github_downloads(mctx, downloader_state, annotations, parsed_packages)

    for mod in mctx.modules:
        for cfg in mod.tags.from_cargo:
            annotations = annotations_by_hub_name[cfg.name]
            effective_cargo_config = cargo_config_by_hub_name[cfg.name]
            use_home_cargo_credentials = cfg.use_home_cargo_credentials or global_use_home_cargo_credentials

            if use_home_cargo_credentials:
                if not effective_cargo_config:
                    fail("Must provide cargo_config or crate.config(cargo_config_toml = ...) when using cargo credentials")

                cargo_credentials = load_cargo_credentials(mctx, effective_cargo_config)
            else:
                cargo_credentials = {}

            cargo_credentials_by_hub_name[cfg.name] = cargo_credentials
            packages = packages_by_hub_name[cfg.name]
            registry_sources = set([
                package["source"]
                for package in packages
                if package.get("source") and package["source"].startswith("sparse+")
            ])

            start_crate_registry_downloads(mctx, downloader_state, annotations, packages, cargo_credentials, cfg.debug)

            for source in sorted(registry_sources):
                registry_config_repository(
                    name = registry_config_repo_name(cfg.name, source),
                    source = source,
                    cargo_config = effective_cargo_config,
                    use_home_cargo_credentials = use_home_cargo_credentials,
                )

    for fetch_state in downloader_state.in_flight_git_crate_fetches_by_url.values():
        fetch_state.download_token.wait()

    download_metadata_for_git_crates(mctx, downloader_state, annotations_by_hub_name)

    facts = {}
    direct_deps = []
    direct_dev_deps = []

    for mod in mctx.modules:
        for cfg in mod.tags.from_cargo:
            if mod.is_root:
                if mctx.is_dev_dependency(cfg):
                    direct_dev_deps.append(cfg.name)
                else:
                    direct_deps.append(cfg.name)

            hub_packages = packages_by_hub_name[cfg.name]
            effective_cargo_config = cargo_config_by_hub_name[cfg.name]
            cargo_credentials = cargo_credentials_by_hub_name[cfg.name]

            annotations = annotations_by_hub_name[cfg.name]

            if cfg.debug:
                for _ in range(25):
                    _generate_hub_and_spokes(mctx, cfg.name, annotations, suggested_annotation_snippet_paths, cargo_path, cfg.cargo_lock, cargo_toml_by_hub_name[cfg.name], hub_packages, cfg.platform_triples, cargo_credentials, effective_cargo_config, cfg.validate_lockfile, cfg.debug, cfg.generate_lint_config, cfg.use_legacy_rules_rust_platforms, visibilities = mod.tags.visibility, target_hosts = cfg.target_hosts, workspace_library_target = cfg.workspace_library_target, root_packages = cfg.packages, root_features = cfg.features, include_dev = cfg.include_dev, default_features = cfg.default_features, dry_run = True)

            facts |= _generate_hub_and_spokes(mctx, cfg.name, annotations, suggested_annotation_snippet_paths, cargo_path, cfg.cargo_lock, cargo_toml_by_hub_name[cfg.name], hub_packages, cfg.platform_triples, cargo_credentials, effective_cargo_config, cfg.validate_lockfile, cfg.debug, cfg.generate_lint_config, cfg.use_legacy_rules_rust_platforms, visibilities = mod.tags.visibility, target_hosts = cfg.target_hosts, workspace_library_target = cfg.workspace_library_target, root_packages = cfg.packages, root_features = cfg.features, include_dev = cfg.include_dev, default_features = cfg.default_features)

    # Lay down the git repos with generated per-crate BUILD overlays.
    git_repos = {}
    for mod in mctx.modules:
        for cfg in mod.tags.from_cargo:
            annotations = annotations_by_hub_name[cfg.name]
            for package in packages_by_hub_name[cfg.name]:
                source = package.get("source", "")
                if not source.startswith("git+"):
                    continue

                remote, commit = parse_git_url(source)
                annotation = annotation_for(annotations, package["name"], package["version"], cfg.name)
                repo_name = _external_repo_for_git_source(cfg.name, remote, commit)
                git_repo = git_repos.get(repo_name)
                if not git_repo:
                    git_repo = {
                        "build_files": {},
                        "gen_binaries": {},
                        "commit": commit,
                        "hub_name": cfg.name,
                        "patch_args": [],
                        "patch_tool": "",
                        "patches": {},
                        "remote": remote,
                        "workspace_cargo_toml": annotation.workspace_cargo_toml,
                    }
                    git_repos[repo_name] = git_repo
                elif git_repo["remote"] != remote or git_repo["commit"] != commit:
                    fail("Git crates from %s at %s and %s at %s produce the same repository name %s" % (
                        git_repo["remote"],
                        git_repo["commit"],
                        remote,
                        commit,
                        repo_name,
                    ))

                strip_prefix = git_crate_strip_prefix(package, facts)
                package_path = _git_crate_package_path(annotation, strip_prefix)
                build_file_path = paths.join(package_path, "BUILD.bazel") if package_path else "BUILD.bazel"
                git_repo["build_files"][build_file_path] = _additive_build_file_content(mctx, annotation)
                if annotation.gen_binaries:
                    git_repo["gen_binaries"][build_file_path] = annotation.gen_binaries

                if annotation.patches:
                    patch_args = annotation.patch_args
                    patch_tool = annotation.patch_tool or ""
                    if git_repo["patches"] and (git_repo["patch_args"] != patch_args or git_repo["patch_tool"] != patch_tool):
                        fail("Git crates from %s use incompatible patch settings" % source)

                    git_repo["patch_args"] = patch_args
                    git_repo["patch_tool"] = patch_tool
                    for patch_file in annotation.patches:
                        git_repo["patches"][str(patch_file)] = patch_file

    for repo_name, git_repo in git_repos.items():
        kwargs = {}
        if git_repo["gen_binaries"]:
            kwargs["gen_binaries"] = git_repo["gen_binaries"]

        git_cargo_workspace_repository(
            name = repo_name,
            build_files = git_repo["build_files"],
            commit = git_repo["commit"],
            hub_name = git_repo["hub_name"],
            patch_args = git_repo["patch_args"],
            patch_tool = git_repo["patch_tool"],
            patches = git_repo["patches"].values(),
            remote = git_repo["remote"],
            workspace_cargo_toml = git_repo["workspace_cargo_toml"],
            **kwargs
        )

    kwargs = dict(
        root_module_direct_deps = direct_deps,
        root_module_direct_dev_deps = direct_dev_deps,
        reproducible = True,
    )

    if hasattr(mctx, "facts"):
        kwargs["facts"] = facts

    return mctx.extension_metadata(**kwargs)

_config = tag_class(
    doc = "Global Cargo configuration for closures that do not provide their own cargo_config.",
    attrs = {
        "cargo_config_toml": attr.label(
            doc = "The Cargo configuration file applied to every closure without cargo_config.",
            mandatory = True,
        ),
        "use_home_cargo_credentials": attr.bool(
            doc = "Load ~/.cargo/credentials.toml for every Cargo closure.",
        ),
    },
)

_from_cargo = tag_class(
    doc = "Generates a repo @crates from a Cargo.toml / Cargo.lock pair.",
    # Ordering is controlled for readability in generated docs.
    attrs = {
        "name": attr.string(
            doc = "The name of the repo to generate",
            default = "crates",
        ),
    } | {
        "cargo_toml": attr.label(
            doc = "The workspace-level Cargo.toml. There can be multiple crates in the workspace.",
        ),
        "cargo_lock": attr.label(),
        "packages": attr.string_list(doc = "Cargo workspace roots. Defaults to workspace default-members, or all members when absent."),
        "features": attr.string_list_dict(doc = "Additional features keyed by selected workspace package name."),
        "include_dev": attr.bool(default = True, doc = "Include development dependencies of selected workspace roots."),
        "default_features": attr.bool(default = True, doc = "Enable default features of selected workspace roots."),
        "cargo_config": attr.label(),
        "generate_lint_config": attr.bool(
            doc = "If true, generate per-package Cargo lint configuration by reading workspace member manifests.",
            default = False,
        ),
        "use_home_cargo_credentials": attr.bool(
            doc = "If set, the ruleset will load `~/cargo/credentials.toml` and attach those credentials to registry requests.",
        ),
        "workspace_library_target": attr.string(doc = "Library target name for workspace member aliases."),
        "target_hosts": attr.string_dict(doc = "Target-to-compiler-host pairs for the workspace JSON index."),
        "platform_triples": attr.string_list(
            mandatory = True,
            doc = "The set of triples to resolve for. They must correspond to the union of any exec/target platforms that will participate in your build.",
        ),
        "use_legacy_rules_rust_platforms": attr.bool(
            doc = "If true, use the legacy rules_rust platforms. If false, use rules_rs platforms.",
            default = False,
        ),
        "validate_lockfile": attr.bool(
            doc = "If true, fail if Cargo.lock versions don't satisfy Cargo.toml requirements.",
            default = True,
        ),
        "debug": attr.bool(),
    },
)

_ANNOTATION_COMMON_ATTRS = {
    "crate": attr.string(
        doc = "The name of the crate the annotation is applied to",
        mandatory = True,
    ),
    "version": attr.string(
        doc = "The version of the crate the annotation is applied to. Defaults to all versions.",
        default = "*",
    ),
    "repositories": attr.string_list(
        doc = "Repository names specified by crate.from_cargo(name=...). Defaults to all repositories.",
    ),
}

_ANNOTATION_SELECTABLE_ATTRS = {
    "build_script_data": attr.label_list(
        doc = "Labels to add to a crate's `cargo_build_script::data` attribute.",
    ),
    "build_script_env": attr.string_dict(
        doc = "Environment variables to add to a crate's `cargo_build_script::env` attribute.",
    ),
    "build_script_tools": attr.label_list(
        doc = "Labels to add to a crate's `cargo_build_script::tools` attribute.",
    ),
    "crate_features": attr.string_list(
        doc = "Features to add to a crate's `rust_library::crate_features` attribute.",
    ),
    "rustc_flags": attr.string_list(
        doc = "Flags to add to a crate's `rust_library::rustc_flags` attribute.",
    ),
}

_annotation = tag_class(
    doc = "A collection of extra attributes and settings for a particular crate.",
    attrs = _ANNOTATION_COMMON_ATTRS | _ANNOTATION_SELECTABLE_ATTRS | {
        "additive_build_file": attr.label(
            doc = "A file containing extra contents to write to the bottom of generated BUILD files.",
        ),
        "additive_build_file_content": attr.string(
            doc = "Extra contents to write to the bottom of generated BUILD files.",
        ),
        # "alias_rule": attr.string(
        #     doc = "Alias rule to use instead of `native.alias()`.  Overrides [render_config](#render_config)'s 'default_alias_rule'.",
        # ),
        # "build_script_data_glob": attr.string_list(
        #     doc = "A list of glob patterns to add to a crate's `cargo_build_script::data` attribute",
        # ),
        # "build_script_deps": attr.label_list(
        #     doc = "A list of labels to add to a crate's `cargo_build_script::deps` attribute.",
        # ),
        "build_script_env_files": attr.label_list(
            doc = "Files containing additional environment variables for a crate's `cargo_build_script`.",
            allow_files = True,
        ),
        "allow_build_script_to_detect_nonhermetic_paths": attr.bool(
            default = False,
            doc = "Allow this crate's build script to emit absolute host-system paths in rustc-link-search, rustc-env, or metadata directives.",
        ),
        # "build_script_link_deps": attr.label_list(
        #     doc = "A list of labels to add to a crate's `cargo_build_script::link_deps` attribute.",
        # ),
        # "build_script_rundir": attr.string(
        #     doc = "An override for the build script's rundir attribute.",
        # ),
        # "build_script_rustc_env": attr.string_dict(
        #     doc = "Additional environment variables to set on a crate's `cargo_build_script::env` attribute.",
        # ),
        "build_script_toolchains": attr.label_list(
            doc = "A list of labels to set on a crate's `cargo_build_script::toolchains` attribute.",
        ),
        "build_script_tags": attr.string_list(
            doc = "A list of tags to add to a crate's `cargo_build_script` target.",
        ),
        # "compile_data": attr.label_list(
        # doc = "A list of labels to add to a crate's `rust_library::compile_data` attribute.",
        # ),
        # "compile_data_glob": attr.string_list(
        # doc = "A list of glob patterns to add to a crate's `rust_library::compile_data` attribute.",
        # ),
        # "compile_data_glob_excludes": attr.string_list(
        # doc = "A list of glob patterns to be excllued from a crate's `rust_library::compile_data` attribute.",
        # ),
        "data": attr.label_list(
            doc = "A list of labels to add to a crate's `rust_library::data` attribute.",
        ),
        # "data_glob": attr.string_list(
        #     doc = "A list of glob patterns to add to a crate's `rust_library::data` attribute.",
        # ),
        "deps": attr.label_list(
            doc = "A list of labels to add to a crate's `rust_library::deps` attribute.",
        ),
        "link_deps": attr.string_list(
            doc = "Labels to add to a crate's `rust_library::link_deps` attribute.",
        ),
        "tags": attr.string_list(
            doc = "A list of tags to add to a crate's generated targets.",
        ),
        # "disable_pipelining": attr.bool(
        #     doc = "If True, disables pipelining for library targets for this crate.",
        # ),
        "extra_aliased_targets": attr.string_dict(
            doc = "A dictionary mapping alias names in the hub repository to target names in the generated crate package.",
        ),
        # "gen_all_binaries": attr.bool(
        #     doc = "If true, generates `rust_binary` targets for all of the crates bins",
        # ),
        "gen_binaries": attr.string_list(
            doc = "The subset of the crate's bins that should get `rust_binary` targets produced. Otherwise build-only packages are resolved for target platforms with default and annotated features; packages already used as target dependencies retain their resolved features.",
        ),
        "gen_build_script": attr.string(
            doc = "An authoritative flag to determine whether or not to produce `cargo_build_script` targets for the current crate. Supported values are 'on', 'off', and 'auto'.",
            values = ["auto", "on", "off"],
            default = "auto",
        ),
        # "override_target_bin": attr.label(
        #     doc = "An optional alternate target to use when something depends on this crate to allow the parent repo to provide its own version of this dependency.",
        # ),
        # "override_target_build_script": attr.label(
        #     doc = "An optional alternate target to use when something depends on this crate to allow the parent repo to provide its own version of this dependency.",
        # ),
        # "override_target_lib": attr.label(
        #     doc = "An optional alternate target to use when something depends on this crate to allow the parent repo to provide its own version of this dependency.",
        # ),
        # "override_target_proc_macro": attr.label(
        #     doc = "An optional alternate target to use when something depends on this crate to allow the parent repo to provide its own version of this dependency.",
        # ),
        "source": attr.string(doc = "Expected exact Cargo.lock source identity for patched packages."),
        "patch_args": attr.string_list(
            doc = "The `patch_args` attribute of a Bazel repository rule. See [http_archive.patch_args](https://docs.bazel.build/versions/main/repo/http.html#http_archive-patch_args)",
        ),
        "patch_tool": attr.string(
            doc = "The `patch_tool` attribute of a Bazel repository rule. See [http_archive.patch_tool](https://docs.bazel.build/versions/main/repo/http.html#http_archive-patch_tool)",
        ),
        "patches": attr.label_list(
            doc = "The `patches` attribute of a Bazel repository rule. See [http_archive.patches](https://docs.bazel.build/versions/main/repo/http.html#http_archive-patches)",
        ),
        "rustc_env": attr.string_dict(
            doc = "Additional variables to set on a crate's `rust_library::rustc_env` attribute.",
        ),
        # "rustc_env_files": attr.label_list(
        #     doc = "A list of labels to set on a crate's `rust_library::rustc_env_files` attribute.",
        # ),
        # "shallow_since": attr.string(
        #     doc = "An optional timestamp used for crates originating from a git repository instead of a crate registry. This flag optimizes fetching the source code.",
        # ),
        "strip_prefix": attr.string(),
        "workspace_cargo_toml": attr.string(
            doc = "For crates from git, the ruleset assumes the (workspace) Cargo.toml is in the repo root. This attribute overrides the assumption.",
            default = "Cargo.toml",
        ),
    },
)

_annotation_select = tag_class(
    doc = "A collection of build attributes applied to a crate for selected platform triples. Source attributes such as patches and workspace_cargo_toml belong on crate.annotation.",
    attrs = _ANNOTATION_COMMON_ATTRS | {
        "triples": attr.string_list(
            doc = "Platform triples to which the annotation applies.",
            mandatory = True,
        ),
    } | _ANNOTATION_SELECTABLE_ATTRS,
)

_visibility = tag_class(
    doc = "Visibility of generated crates and hub aliases. Generated dependencies remain accessible within the Cargo closure; package metadata remains public.",
    attrs = {
        "crates": attr.string_list(mandatory = True, doc = "Exact Cargo names or prefixes ending in *; overlapping settings are rejected."),
        "repositories": attr.string_list(doc = "Hub names. Empty applies to every hub declared by this module."),
        "visibility": attr.label_list(mandatory = True, doc = "Allowed consumer packages or package groups. Labels resolve in the declaring module."),
    },
)

crate = module_extension(
    implementation = _crate_impl,
    tag_classes = {
        "annotation": _annotation,
        "annotation_select": _annotation_select,
        "config": _config,
        "from_cargo": _from_cargo,
        "visibility": _visibility,
    },
)

def _hub_repo_impl(rctx):
    for path, contents in rctx.attr.contents.items():
        rctx.file(path, contents)
    rctx.file("REPO.bazel", "")

_hub_repo = repository_rule(
    implementation = _hub_repo_impl,
    attrs = {
        "contents": attr.string_dict(
            doc = "A mapping of file names to text they should contain.",
            mandatory = True,
        ),
    },
)
